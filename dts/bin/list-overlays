#!/usr/bin/perl

use v5.16;
use strict;
use warnings qw( FATAL all );
use autodie;
use Fcntl qw( LOCK_EX );

chdir '/sys/kernel/config/device-tree/overlays';

my @applied;

opendir my $dh, '.';
open my $dh_fh, '+<&', fileno( $dh );
flock $dh_fh, LOCK_EX;

for( readdir $dh ) {
	next if /^\.\.?\z/;
	open my $fh, '<', "$_/status";
	chomp( my $status = <$fh> );
	if( $status eq 'applied' ) {
		push @applied, $_;
	} elsif( eval { rmdir $_ } ) {
		say "Removed stale overlay $_\n";
	} else {
		warn "Unable to remove stale overlay $_\n";
	}
}
closedir $dh;
close $dh_fh;

say "Overlays applied: @applied"  if @applied;
say "No overlays applied"  unless @applied;
